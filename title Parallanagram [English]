title Parallanagram [English]
author Stephen Lavelle
homepage www.increpare.com

background_color darkgreen
text_color gray

verbose_logging

run_rules_on_level_start

norepeat_action

again_interval 1.0

========
OBJECTS
========

Background
black


player 
yellow
0...0
.....
.....
.....
0...0

backred 
darkred

backblue 
blue

A
black
0...0
.000.
.....
.000.
.000.

B
black
....0
.000.
....0
.000.
....0

C
black
.....
.0000
.0000
.0000
.....

D
black
....0
.000.
.000.
.000.
....0

E
black
.....
.0000
...00
.0000
.....

F
black
.....
.0000
...00
.0000
.0000

G
black
....0
.0000
.0...
.00.0
....0

H
black
.000.
.000.
.....
.000.
.000.

I
black
0...0
00.00
00.00
00.00
0...0

J
black
00...
000.0
000.0
.00.0
0..00

K
black
.00..
.0.00
..000
.0.00
.00..

L
black
.0000
.0000
.0000
.0000
.....

M
black
.....
.0.0.
.0.0.
.0.0.
.0.0.

N
black
...0.
.0.0.
.0.0.
.0.0.
.0...

O
black
0...0
.000.
.000.
.000.
0...0

P
black
.....
.000.
.....
.0000
.0000

Q
black
.....
.000.
.....
0000.
0000.

R
black
.....
.000.
....0
.000.
.000.

S
black
.....
.0000
.....
0000.
.....

T
black
.....
00.00
00.00
00.00
00.00

U
black
.000.
.000.
.000.
.000.
0...0

V
black
.000.
.000.
0.0.0
0.0.0
00.00

W
black
.0.0.
.0.0.
.0.0.
.0.0.
0.0.0

X
black
.000.
0.0.0
00.00
0.0.0
.000.

Y
black
.000.
.000.
.....
0000.
.....

Z
black
.....
000.0
00.00
0.000
.....


1
black
.00.0
0..00
.00.0
....0
.00.0

9
black
.00.0
00000
0..00
.00.0
0..00

7
black
.00.0
00000
.00.0
.00.0
0..00

2
black
...00
.00.0
.0.00
.00.0
.0.00


cover 
darkgreen

lmargin
darkblue 
0....
0....
0....
0....
0....


rmargin
darkblue 
....0
....0
....0
....0
....0

bmargin
darkblue
.....
.....
.....
.....
00000

tmargin
darkblue
00000
.....
.....
.....
.....

blank -
black

tl /
transparent

endbg
lightbrown

oendbg 
orange

=======
LEGEND
=======

letter = a or b or c or d or e or f or g or h or i or j or k or l or m or n or o or p or q or r or s or t or u or v or w or x or y or z or 1 or 2 or 7 or 9
. = Background
0 = player and blank


=======
SOUNDS
=======

endlevel 54461103
endgame 54461103

startgame 54461103

================
COLLISIONLAYERS
================

Background
tl
backred, backblue
endbg,
oendbg
letter, blank
cover
Player
tmargin
bmargin
lmargin
rmargin

======
RULES
======

[tl] -> [ backred ]
[backred | no backblue] -> [ backred | backblue ]
+[backblue | no backred] -> [backblue | backred ]

[ > player | cover ] -> cancel

[action player no letter  ] [letter no endbg ]-> cancel
down [ action player letter | cover ] -> [ action player up letter | cover ]
up [ action player letter | cover ] -> [ action player down letter | cover ]

right [action player ] [ moving letter  no cover | ... | cover ] -> [action player ] [ | ... | moving letter cover ]
[action player ] [ > letter | ] -> [action player ]  [ | letter ]

right [action player ] [ no cover no letter | letter ] -> [action player ] [ letter | ]

[action player ] [ no letter no cover ] ->[action player ]  [ blank ]
[action player ] [ no cover no letter ] ->[action player ]  [ blank ]

[endbg] [action player] ->[ endbg ] [] win
[endbg] [moving player] ->[ endbg ] [] win

late [ cover ] -> [ ]
late [ no letter no blank ] -> [ cover ]


right late [ cover | d|u|e|l | cover ] -> [ cover  endbg | d endbg |u endbg |e endbg |l  endbg | cover ] again
right late [ cover | d|i|t|c|h | cover ] -> [ cover  endbg | d endbg |i endbg |t endbg |c endbg |h  endbg | cover ] again
right late [ cover | l|o|n|g|e|r | cover ] -> [ cover  endbg | l endbg |o endbg |n endbg |g endbg |e endbg |r  endbg | cover ]  again
right late [ cover | l|i|k|e|n|e|d| cover ] -> [ cover  endbg | l endbg |i endbg |k endbg |e endbg |n endbg |e endbg |d endbg | cover ]  again
right late [ cover | u|r|b|a|n|i|z|e | cover ] -> [ cover  endbg | u endbg |r endbg |b endbg |a endbg |n endbg |i endbg |z endbg |e  endbg | cover ] again
right late [ cover | t|r|e|a|s|u|r|e|d | cover ] -> [ cover  endbg | t endbg |r endbg |e endbg |a endbg |s endbg |u endbg |r endbg |e endbg |d  endbg | cover ]  again
right late [ cover | e|n|t|h|r|a|l|l|e|d| cover ] -> [ cover  endbg | e endbg |n endbg |t endbg |h endbg |r endbg |a endbg |l endbg |l endbg |e endbg |d endbg | cover ] again
right late [ cover | i|n|f|e|s|t|a|t|i|o|n | cover ] -> [ cover  endbg | i endbg |n endbg |f endbg |e endbg |s endbg |t endbg |a endbg |t endbg |i endbg |o endbg |n  endbg | cover ]  again

late [endbg] [letter]->[endbg] [ letter endbg]

late right [endbg  no oendbg | endbg  ]->[endbg | endbg oendbg ]

late up [ cover | no cover ] -> [ cover tmargin | ]
late down [ cover | no cover ] -> [ cover bmargin | ]
late left [ cover | no cover ] -> [ cover lmargin | ]
late right [ cover | no cover ] -> [ cover rmargin | ]
==============
WINCONDITIONS
==============


=======
LEVELS
=======

message 4 of 11
message We have found a way to massively speed up anagram computation!

/..........
.dleu.bhal.
.0---.0---.
...........
.lhsa.bkla.
.0---.0---.
...........


message 5 of 11

/............
.tidch.roged.
.0----.0----.
.............
.enked.onsts.
.0----.0----.
.............
.mdait.......
.0----.......
.............


message 6 of 11

/..............
.rneolg.tsiagl.
.0-----.0-----.
...............
.tgeerr.tbeogl.
.0-----.0-----.
...............
.daengw.soelgv.
.0-----.0-----.
...............


message 7 of 11

/................
.klndeie.ecihrhs.
.0------.0------.
.................
.nvaetig.npssiee.
.0------.0------.
.................
.gossarm.nhgndau.
.0------.0------.
.................
.gfrduie.........
.0------.........
.................


message 8 of 11

/..................
.enbairzu.mwkeaurl.
.0-------.0-------.
...................
.storlhec.iaesuhrt.
.0-------.0-------.
...................
.sreoihet.shateerf.
.0-------.0-------.
...................
.sldugnei.gkeairnf.
.0-------.0-------.
...................


message 9 of 11

/..............................
.edutsrrae.vsngrooer.ttibheics.
.0--------.0--------.0--------.
...............................
.esiarsnum.elneetlma.egwsaiesn.
.0--------.0--------.0--------.
...............................
.nsalugage.cshepenir.ostpeihnc.
.0--------.0--------.0--------.
...............................


message 10 of 11

/.................................
.nhetelrlad.niataepdon.iginfaenrl.
.0---------.0---------.0---------.
..................................
.rietakcohs.oerrfeings.hoaetilgon.
.0---------.0---------.0---------.
..................................
.oelvgbrane.eslnmbuare............
.0---------.0---------............
..................................
.oirmnontas.oaenmirhcs............
.0---------.0---------............
..................................


message 11 of 11

/....................................
.nfoaitnstei.ulitnecihtm.etdlnrseahn.
.0----------.0----------.0----------.
.....................................
.eaegtidshrn.ackeineilhm.urcrneehatf.
.0----------.0----------.0----------.
.....................................
.eonniegitrr.onermhscoom.elinsiskeet.
.0----------.0----------.0----------.
.....................................
.esenzrdeitw.ileattstned.............
.0----------.0----------.............
.....................................

message THE END. Congratulations.